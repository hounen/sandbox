C==== ANGULAR INTEGRATION ==============================================
      SUBROUTINE SET_ANGLE
      INCLUDE 'DIM_INC.f'
      INCLUDE 'GEM_INC.f'
      DO 1000 LL1=0,12
      DO 1000 LL2=0,12
      DO 1000 LL3=0,12
      DO 1000 LL4=0,12
      DO 1000 LAM=0,12
      DO 1000 LTT=0,6
        CWFAC(LL1,LL2,LL3,LL4,LAM,LTT)=0.D0
1000  CONTINUE

      DO 1001 LTT=0,6
      DO 1001 LL1=0,12
      DO 1001 LL2=0,12
        IF(IABS(LL1-LL2).GT.LTT.OR.LL1+LL2.LT.LTT) GOTO 1001
      DO 1002 LL3=0,12
      DO 1002 LL4=0,12
        IF(IABS(LL3-LL4).GT.LTT.OR.LL3+LL4.LT.LTT) GOTO 1002
      DO 1003 LAM=0,12
        IF(IABS(LL1-LL3).GT.LAM.OR.LL1+LL3.LT.LAM) GOTO 1003
        IF(IABS(LL2-LL4).GT.LAM.OR.LL2+LL4.LT.LAM) GOTO 1003
        CWFAC(LL1,LL2,LL3,LL4,LAM,LTT)
     &    =(-1.D0)**(LL2+LL3+LTT)*(2.D0*LAM+1.D0)
     &    *DSQRT((2.D0*LL3+1.D0)*(2.D0*LL4+1.D0))
     &    *CGZ_I(LL3,LAM,LL1)*CGZ_I(LL4,LAM,LL2)
     &    *SIXJ_I(LTT,LL2,LL1,LAM,LL3,LL4)
1003  CONTINUE
1002  CONTINUE
1001  CONTINUE

      DO 2000 II=1,8
      DO 2000 LL1=0,12          
      DO 2000 LL2=0,12          
      DO 2000 LL3=0,12
      DO 2000 LL4=0,12
      DO 2000 LT12=0,6
      DO 2000 LT34=0,6
        SOFAC(II,LL1,LL2,LL3,LL4,LT12,LT34)=0.D0
2000  CONTINUE

      DO 2001 LL1=0,12
      DO 2001 LL2=0,12
      DO 2001 LT12=0,6
        IF(IABS(LL1-LL2).GT.LT12.OR.LL1+LL2.LT.LT12) GOTO 2001
      DO 2002 LL3=0,12
      DO 2002 LL4=0,12
      DO 2002 LT34=0,6
        IF(IABS(LL3-LL4).GT.LT34.OR.LL3+LL4.LT.LT34) GOTO 2002
        IF(LL1.EQ.LL3.AND.LL2.EQ.LL4) THEN
          SOFAC(1,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =(-1.D0)**(LL1+LL2+LT34+1)
     &      *DSQRT((2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &      *DSQRT(LL1*(LL1+1.D0)*(2.D0*LL1+1.D0))
     &      *SIXJ_I(LL1,LT12,LL2,LT34,LL1,1)
          SOFAC(2,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =(-1.D0)**(LL1+LL2+LT12+1)
     &      *DSQRT((2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &      *DSQRT(LL2*(LL2+1.D0)*(2.D0*LL2+1.D0))
     &      *SIXJ_I(LL2,LT12,LL1,LT34,LL2,1)
        ENDIF
        IF(IABS(LL1-LL3).GT.1.OR.LL1+LL3.LT.1) GOTO 2002
        IF(IABS(LL2-LL4).GT.1.OR.LL2+LL4.LT.1) GOTO 2002
        IF(LL2+1.EQ.LL4) THEN
          SOFAC(3,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =DSQRT(6.D0*(2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &      *DSQRT(2.D0*LL3+1.D0)*CGZ_I(LL3,1,LL1)
     &      *DNINJ_I(1,1,1,LL1,LL2,LT12,LL3,LL4,LT34)
        ENDIF
        IF(LL2-1.EQ.LL4) THEN
          SOFAC(4,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =DSQRT(6.D0*(2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &      *DSQRT(2.D0*LL3+1.D0)*CGZ_I(LL3,1,LL1)
     &      *DNINJ_I(1,1,1,LL1,LL2,LT12,LL3,LL4,LT34)
          SOFAC(5,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =SOFAC(4,LL1,LL2,LL3,LL4,LT12,LT34)
        ENDIF
        IF(LL1+1.EQ.LL3) THEN
          SOFAC(6,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =DSQRT(6.D0*(2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &      *DSQRT(2.D0*LL4+1.D0)*CGZ_I(LL4,1,LL2)
     &      *DNINJ_I(1,1,1,LL1,LL2,LT12,LL3,LL4,LT34)
        ENDIF
        IF(LL1-1.EQ.LL3) THEN
          SOFAC(7,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =DSQRT(6.D0*(2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &      *DSQRT(2.D0*LL4+1.D0)*CGZ_I(LL4,1,LL2)
     &      *DNINJ_I(1,1,1,LL1,LL2,LT12,LL3,LL4,LT34)
          SOFAC(8,LL1,LL2,LL3,LL4,LT12,LT34)
     &      =SOFAC(7,LL1,LL2,LL3,LL4,LT12,LT34)
        ENDIF
2002  CONTINUE
2001  CONTINUE

      DO 3000 II=0,2
      DO 3000 LL1=0,12
      DO 3000 LL2=0,12
      DO 3000 LL3=0,12
      DO 3000 LL4=0,12
      DO 3000 LT12=0,6
      DO 3000 LT34=0,6
        TSFAC(II,LL1,LL2,LL3,LL4,LT12,LT34)=0.D0
3000  CONTINUE

      DO 3001 LT12=0,6
      DO 3001 LT34=0,6
        IF(IABS(LT12-LT34).GT.2.OR.LT12+LT34.LT.2) GOTO 3001
      DO 3002 LL1=0,12
      DO 3002 LL2=0,12
        IF(IABS(LL1-LL2).GT.LT12.OR.LL1+LL2.LT.LT12) GOTO 3002
      DO 3003 LL3=0,12
      DO 3003 LL4=0,12
        IF(IABS(LL3-LL4).GT.LT34.OR.LL3+LL4.LT.LT34) GOTO 3003
      DO 3004 II=0,2
        IF(IABS(LL1-LL3).GT.II  .OR.LL1+LL3.LT.II)   GOTO 3004
        IF(IABS(LL2-LL4).GT.2-II.OR.LL2+LL4.LT.2-II) GOTO 3004
        TSFAC(II,LL1,LL2,LL3,LL4,LT12,LT34)
     &    =0.5D0/DSQRT(PI)
     &    *DSQRT(FAC(5)/FAC(2*II)/FAC(4-2*II))
     &    *DSQRT(5.D0*(2.D0*LT12+1.D0)*(2.D0*LT34+1.D0))
     &    *DSQRT((2.D0*LL3+1.D0)*(2.D0*LL4+1.D0))
     &    *CGZ_I(LL3,II,LL1)*CGZ_I(LL4,2-II,LL2)
     &    *DNINJ_I(II,2-II,2,LL1,LL2,LT12,LL3,LL4,LT34)
3004  CONTINUE
3003  CONTINUE
3002  CONTINUE
3001  CONTINUE
      RETURN
      END
